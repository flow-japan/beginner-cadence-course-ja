# Глава 3 День 1 - Ресурсы

О-о-о. Мы подошли к самой важной теме во всем Cadence: Ресурсы. Серьезно, это самое важное, что вы узнаете от меня. Давайте приступим!

## Видео

1. Вы можете посмотреть это видео с 00:00 до 08:00 (остальное мы раберем позже): https://www.youtube.com/watch?v=SGa2mnDFafc

## Ресурсы

<img src="../images/resources.jpeg" alt="drawing" width="500" />

Ресурсы - это, вероятно, самый важный элемент Cadence, и причина, по которой Cadence настолько уникален. Судя по их виду, **ресурс - это более надежная структура**. Это простой способ выразить это. Но что более важно, благодаря своей защищенности, они используются многими интересными способами, которые мы рассмотрим.

Всегда полезно посмотреть на код, поэтому давайте сначала сделаем это:
```cadence
pub resource Greeting {
    pub let message: String
    init() {
        self.message = "Hello, Mars!"
    }
}
```

Не правда ли, это очень похоже на структуру? В коде они действительно выглядят очень похоже. Здесь ресурс `Greeting` - это контейнер, хранящий сообщение, которое представляет собой тип `String`. Но за кулисами есть много, много различий.

### Ресурсы vs. Структуры

В Cadence структуры - это просто контейнеры данных. Вы можете копировать их, перезаписывать и создавать, когда захотите. Все это совершенно не подходит для ресурсов. Вот некоторые важные факты, определяющие ресурсы:

1. Их нельзя скопировать
2. Их нельзя потерять (или перезаписать)
3. Их нельзя создать, когда вам захочется
4. Вы должны быть *очень* конкретными в отношении того, как вы обращаетесь с ресурсом (например, перемещаете его).
5. С ресурсами гораздо сложнее работать

Давайте посмотрим на приведенный ниже код, чтобы разобраться с ресурсами:
```cadence
pub contract Test {

    pub resource Greeting {
        pub let message: String
        init() {
            self.message = "Hello, Mars!"
        }
    }

    pub fun createGreeting(): @Greeting {
        let myGreeting <- create Greeting()
        return <- myGreeting
    }
}
```

Здесь происходит очень много важных событий, поэтому давайте рассмотрим их поэтапно:

1. Мы инициализируем тип ресурса `Greeting`, который содержит поле `message`. Вы уже знаете это.
2. Мы определяем функцию `createGreeting`, которая возвращает ресурс `Greeting`. Обратите внимание, что ресурсы в Cadence используют символ `@` перед своим типом, чтобы сказать: "Это ресурс".
3. Мы создаем новый тип `Greeting` с помощью ключевого слова `create` и присваиваем ему `myGreeting` с помощью оператора `<-` "move". В Cadence вы не можете просто использовать `=`, чтобы поместить ресурс куда-либо. Вы ДОЛЖНЫ использовать оператор `<-` "move operator" для явного "перемещения" ресурса.
4. Мы возвращаем новое `Greeting`, снова перемещая ресурс в возвращаемое значение.

Хорошо, это здорово. Но что если мы *хотим* уничтожить ресурс? Ну, мы можем сделать это довольно легко:

```cadence
pub contract Test {

    pub resource Greeting {
        pub let message: String
        init() {
            self.message = "Hello, Mars!"
        }
    }

    pub fun makeAndDestroy() {
        let myGreeting <- create Greeting()
        destroy myGreeting 
        // Note: This is the only time you don't use the 
        // `<-` operator to change the location of a resource.
    }
}
```

Вы уже видите, что ресурсы сильно отличаются от структур. Мы должны быть гораздо более коммуникабельными при работе с ресурсами. Давайте рассмотрим некоторые вещи, которые мы не можем делать с ресурсами:
```cadence
pub contract Test {

    pub resource Greeting {
        pub let message: String
        init() {
            self.message = "Hello, Mars!"
        }
    }

    pub fun createGreeting(): @Greeting {
        let myGreeting <- create Greeting()

        /*
            myGreeting <- create Greeting()

            Вы не можете сделать вышеуказанное. Это приведет к "перезаписи" 
            переменной myGreeting и фактически потеряет 
            предыдущий ресурс, который уже хранится там.
        */

        /*
            let copiedMyGreeting = myGreeting

            Вы не можете сделать вышеуказанное. Это приведет к попытке "скопировать"
            ресурс myGreeting, что недопустимо. 
            Ресурсы никогда не могут быть скопированы. Если вы хотите
            переместить myGreeting "в" скопированное MyGreeting,
            вы можете сделать следующее:

            let copiedMyGreeting <- myGreeting

            После этого myGreeting не будет хранить ничего 
            внутри него, так что вы больше не сможете его использовать.
        */

        /*
            return myGreeting
            
            Вы не можете сделать вышеуказанное. Вы должны явно "переместить" 
            ресурс с помощью оператора <-, как мы делаем ниже.
        */
        return <- myGreeting
    }
}
```

Итак, почему это полезно? Разве это не просто чертовски раздражает? Нет, хаха. На самом деле это очень полезно. Допустим, мы хотим дать кому-то NFT стоимостью в миллиарды долларов. Разве мы не хотим быть уверены, что не потеряем этот NFT? Мы можем сделать это в Cadence, потому что *так трудно* потерять наш Ресурс, если мы БУКВАЛЬНО не скажем ему уничтожить. Это играет в общую тему Cadence: **Cadence делает так, что разработчику очень трудно ошибиться. И это хорошо.**

Вот краткое описание различий между ними:
- Структуры - это контейнеры для данных. Вот и все.
- Ресурсы - это чрезвычайно безопасные, трудно теряемые, не копируемые, хорошо сохраняемые контейнеры с данными, которые невозможно потерять.

## Несколько примечаний по кодингу

Вот несколько примечаний, которые следует выучить, для работы с ресурсами:

- Создать новый ресурс можно только с помощью ключевого слова `create`. Ключевое слово `create` может быть использовано только внутри контракта. Это означает, что вы, как разработчик, можете контролировать, когда они будут созданы. Это не относится к структурам, поскольку структуры могут быть созданы вне контракта.
- Вы должны использовать символ `@` перед типом ресурса, например: `@Greeting`.
- Для перемещения ресурса используется символ `<-`.
- Используйте ключевое слово `destroy`, чтобы уничтожить ресурс.

## Это было не так уж и страшно?

Эй, вы сделали это! Это было не так уж плохо, верно? Я думаю, вы все справитесь. Давайте на этом закончим на сегодня ;)

## Квесты

Как всегда, не стесняйтесь отвечать на выбранном вами языке.

1. Перечислите 3 признака, по которым структуры отличаются от ресурсов.

2. Опишите ситуацию, когда ресурс может быть лучше, чем структура.

3. Какой ключевой символ нужно использовать для создания ресурса?

4. Можно ли создать ресурс в скрипте или транзакции (при условии, что нет публичной функции для его создания)?

5. К какому типу относится нижеуказанный ресурс?

```cadence
pub resource Jacob {

}
```

6. Давайте поиграем в детскую игру "Я шпион". В этом коде 4 ошибки. Пожалуйста, исправьте их.

```cadence
pub contract Test {

    // Подсказка: здесь все в порядке ;)
    pub resource Jacob {
        pub let rocks: Bool
        init() {
            self.rocks = true
        }
    }

    pub fun createJacob(): Jacob { // одна здесь
        let myJacob = Jacob() // две здесь
        return myJacob // и одна здесь
    }
}
```